# Maintainer: Yusuke Sasaki <y_sasaki@nuem.nagoya-u.ac.jp>

_realname=gurobi
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=6.5.2
pkgrel=1
pkgdesc="MinGW-w64 library for Gurobi (be careful to use)"
arch=('any')
url=""
license=('custom')
makedepends=("${MINGW_PACKAGE_PREFIX}-tools-git")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
source=('Makefile')
sha256sums=('SKIP')
options=('!strip' 'staticlibs')

_verno=${pkgver//\./}
_mverno=${_verno/%?/}
_gurobi_home="$(cygpath $GUROBI_HOME)"

prepare() {
  # check Gurobi installation
  if [[ ! -d $(cygpath $GUROBI_HOME) ]]; then
    echo "\$GUROBI_HOME is not set."
    return 1
  fi
  verno=$(basename $(dirname `cygpath $GUROBI_HOME`) | sed 's/gurobi//')
  if ! [[ $verno = ${_verno} ]]; then
    echo "Gurobi version mismatch"
    return 1
  fi 

  # copy the dependencies from working directory (be careful).
  cp -r "${_gurobi_home}"/src/cpp                  ${srcdir}/src
  cp    "${_gurobi_home}"/include/gurobi_{c,c++}.h ${srcdir}/src

  # generate def file.
  gendef - "${_gurobi_home}"/bin/gurobi${_mverno}.dll \
    > ${srcdir}/gurobi${_mverno}.def

  mv Makefile src/
}

build() {
  cd ${srcdir}
  dlltool --dllname gurobi${_mverno}.dll \
          --def gurobi${_mverno}.def \
          --output-lib libgurobi${_mverno}.dll.a

  cd ${srcdir}/src
  make
}

package() {
  cd ${srcdir}

  local _prefix="${pkgdir}${MINGW_PREFIX}"

  mkdir -p ${_prefix}/{bin,include,lib}

  cp ${_gurobi_home}/bin/{gurobi,aes}${_mverno}.dll "${_prefix}/bin/"
  cp ${_gurobi_home}/include/gurobi_{c,c++}.h       "${_prefix}/include/"
  cp $srcdir/libgurobi${_mverno}.dll.a              "${_prefix}/lib/"
  cp $srcdir/libgurobi_c++.a                        "${_prefix}/lib/"
}

# vim: set ft=sh ts=2 sw=2 et :
