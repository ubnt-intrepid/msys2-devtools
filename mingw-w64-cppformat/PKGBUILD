# Maintainer: Yusuke Sasaki <y_sasaki@nuem.nagoya-u.ac.jp>

_realname=cppformat
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.0.0
pkgrel=1
pkgdesc="Small, safe and fast formatting library for C++"
arch=('any')
url="https://cppformat.github.io"
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake")
depends=()
source=(
  "https://github.com/cppformat/${_realname}/archive/${pkgver}.tar.gz"
)
md5sums=(SKIP)

build() {
  cd "${_realname}-${pkgver}"

  rm -rf build
  mkdir build
  cd build

  cmake -G "MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=1 \
    -DFMT_TEST=0 \
    -Wno-dev \
    ..
  make
}

check() {
  cd "${_realname}-${pkgver}/build"

  cmake -G "MSYS Makefiles" \
    -DFMT_TEST=1 \
    -Wno-dev \
    ..
  make
  make test
}

package() {
  cd "${_realname}-${pkgver}"
  cd build

  make CMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/${MINGW_PREFIX}/share/doc/${_realname}/{.buildinfo,.doctrees,_sources}
}

# vim: set ft=sh ts=2 sw=2 et :
