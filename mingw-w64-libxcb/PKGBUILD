_realname=libxcb
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.11.1
pkgrel=1
pkgdesc="X11 client-side library (mingw-w64)"
arch=(any)
license=('custom')
url='http://xorg.freedesktop.org/'
source=("http://xcb.freedesktop.org/dist/${_realname}-${pkgver}.tar.bz2"
        0010-no-pthread-stubs.patch
        0020-winsock.patch)
sha256sums=('b720fd6c7d200e5371affdb3f049cc8f88cff9aed942ff1b824d95eedbf69d30'
            SKIP
            SKIP)

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Np1 -i ../0010-no-pthread-stubs.patch
  patch -Np1 -i ../0020-winsock.patch
  autoreconf -vfi
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --enable-xkb \
    --enable-xinput \
    --disable-static
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim: set ft=sh ts=2 sw=2 et :
