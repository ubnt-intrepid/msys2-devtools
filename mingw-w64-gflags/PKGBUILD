# Maintainer: Yusuke Sasaki <y_sasaki@nuem.nagoya-u.ac.jp>

_realname=gflags
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.1.2
pkgrel=1
pkgdesc="C++ Library for commandline flag processing (mingw-w64)"
arch=('any')
url="https://github.com/schuhschuh/gflags"
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake")
source=("https://github.com/schuhschuh/gflags/archive/v${pkgver}.tar.gz"
        '0010-fix-ostype.patch')
sha256sums=('d8331bd0f7367c8afd5fcb5f5e85e96868a00fd24b7276fa5fcee1e5575c2662'
            SKIP)

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/0010-fix-ostype.patch"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  [[ -d build ]] && rm -rf build
  mkdir build && cd build

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -G "Unix Makefiles" \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DCMAKE_BUILD_TYPE=Release \
      -DBUILD_SHARED_LIBS=ON \
      -DBUILD_STATIC_LIBS=ON \
      -DBUILD_TESTING=ON \
      ..

  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  cd build
  make DESTDIR="$pkgdir" install
  mkdir $pkgdir$MINGW_PREFIX/bin
  mv "$pkgdir$MINGW_PREFIX/lib/"*.dll "$pkgdir$MINGW_PREFIX/bin"

  # install -Dm644 COPYING.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING.txt"
}

# vim: set ft=sh ts=2 sw=2 et :
