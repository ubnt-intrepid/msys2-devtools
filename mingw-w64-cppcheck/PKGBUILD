# Maintainer: Yusuke Sasaki <y_sasaki@nuem.nagoya-u.ac.jp>

_realname=cppcheck
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.72
pkgrel=1
pkgdesc="static analysis of C/C++ code (mingw-w64)"
arch=('any')
url="http://cppcheck.sourceforge.net/"
license=('GPL')
depends=("${MINGW_PACKAGE_PREFIX}-qt5"
         "${MINGW_PACKAGE_PREFIX}-python3"
         "${MINGW_PACKAGE_PREFIX}-python3-pygments")
makedepends=("${MINGW_PACKAGE_PREFIX}-docbook-xsl")
source=("https://github.com/danmar/cppcheck/archive/${pkgver}.tar.gz"
        '0010-fix-shebang.patch'
        '0020-change-language-files-path.patch')
sha256sums=('c718949e1ec22a8a0dca7e2953a55c502ef65e53ff9922fb91759388618faa7f'
            'b7d4056522b42b5e99ba990e1e2af4dfca857701aa7c110a59766ff47230b717'
            'a15a73396a2fa346d29df52c3143b46618454d0bc0f12f0ad891d5a4a856cb56')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/0010-fix-shebang.patch"
  patch -p1 -i "${srcdir}/0020-change-language-files-path.patch"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  LANG='en_US.UTF-8' make \
    SRCDIR=build \
    CFGDIR=${MINGW_PREFIX}/share/cppcheck/cfg \
    HAVE_RULES=yes

  make man \
    DB2MAN=${MINGW_PREFIX}/share/xml/docbook/xsl-stylesheets-1.78.1/manpages/docbook.xsl \
    CFGDIR=${MINGW_PREFIX}/share/cppcheck/cfg

  cd gui
  lrelease gui.pro
  qmake HAVE_RULES=yes
  make \
    SRCDIR=build \
    CFGDIR=${MINGW_PREFIX}/share/cppcheck/cfg \
    HAVE_RULES=yes
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  LANG='en_US.UTF-8' make test \
    SRCDIR=build \
    CFGDIR=${MINGW_PREFIX}/share/cppcheck/cfg \
    HAVE_RULES=yes
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  LANG='en_US.UTF-8' make install \
    DESTDIR="${pkgdir}" \
    PREFIX="${MINGW_PREFIX}" \
    SRCDIR=build \
    CFGDIR=${MINGW_PREFIX}/share/cppcheck/cfg \
    HAVE_RULES=yes

    # GUI
    install -m755 build/gui/cppcheck-gui.exe "${pkgdir}${MINGW_PREFIX}/bin"
    install -d "${pkgdir}${MINGW_PREFIX}/share/cppcheck/cfg/lang"
    install -D cfg/* -t "${pkgdir}${MINGW_PREFIX}/share/cppcheck/cfg"
    install -D gui/*.qm -t "${pkgdir}${MINGW_PREFIX}/share/cppcheck/cfg/lang"
}

# vim: set ft=sh ts=2 sw=2 et :
