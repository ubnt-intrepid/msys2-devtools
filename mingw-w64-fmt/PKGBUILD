# Maintainer: Yusuke Sasaki <y_sasaki@nuem.nagoya-u.ac.jp>

_realname=fmt
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.0.0
pkgrel=1
pkgdesc="Small, safe and fast formatting library"
arch=('any')
url="http://fmtlib.net/"
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-python3-sphinx")
source=("https://github.com/fmtlib/${_realname}/archive/${pkgver}.tar.gz")
sha256sums=(f075db25588166d028bd26b931a89b96f2a29f039dfe7c310d235797221c96a1)

prepare() {
  cd "${_realname}-${pkgver}"
}

build() {
  cd "${_realname}-${pkgver}"

  rm -rf build
  mkdir build
  cd build

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -G "Unix Makefiles" \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DCMAKE_BUILD_TYPE=Release \
      -DBUILD_SHARED_LIBS=1 \
      -DFMT_TEST=0 \
      -Wno-dev \
      ..

  make
  make doc
}

check() {
  cd "${_realname}-${pkgver}/build"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    cmake -G "Unix Makefiles" \
      -DFMT_TEST=1 \
      -Wno-dev \
      ..

  make
  make test
}

package() {
  cd "${_realname}-${pkgver}"
  cd build

  make DESTDIR="${pkgdir}" install
}

# vim: set ft=sh ts=2 sw=2 et :
